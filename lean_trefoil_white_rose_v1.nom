###  CHS 7/16/2024 :  "Lean Trefoil"
###  Charles Yushi Cai 11/2/2024 : "The White Rose"

######## Some Surface colors #############################
surface M  color  (0.9 0.1 1  ) endsurface   # Magenta
surface Z  color  (1   0   0.6) endsurface   # Zinnober
surface R  color  (0.9 0.1 0  ) endsurface   # Red
surface O  color  (1   0.6 0  ) endsurface   # Orange
surface Y  color  (0.8 0.8 0  ) endsurface   # Yellow
surface L  color  (0.5 0.8 0  ) endsurface   # Lime
surface G  color  (0.1 0.8 0  ) endsurface   # Green
surface A  color  (0   0.9 0.8) endsurface   # Aqua
surface C  color  (0   0.8 0.9) endsurface   # Cyan
surface U  color  (0   0.6 1  ) endsurface   # Uniform
surface B  color  (0   0.3 1  ) endsurface   # Blue
surface V  color  (0.5 0.1 1  ) endsurface   # Violet
surface P  color  (0.7 0   0.9) endsurface   # Purple
surface LR color (240/255 128/255 128/255) endsurface #Light Red
surface PI color (240/255 186/255 203/255) endsurface #Pink
surface BR color (139/255 69/255 19/255) endsurface #Brown
surface GR color (105/255 105/255 105/255) endsurface #Gray
surface CPR color (184/255 134/255 11/255) endsurface #Copper
surface ZC color (218/255 165/255 32/255) endsurface #Zinc
surface LG color (204/255 255/255 204/255) endsurface #Light Green
surface W  color  (0.92   0.92   0.92  ) endsurface   # White
surface D  color  (0.8 0.8 0.8) endsurface   # Dark
surface K  color  (0   0   0  ) endsurface   # blacK

backgroundcolor (0 0 0) endbackgroundcolor

### reference axes:
point origin (0 0 0) endpoint
point px (0.4 0 0) endpoint
point py (0 0.4 0) endpoint
point pz (0 0 0.4) endpoint

polyline lineX (origin px) endpolyline
polyline lineY (origin py) endpolyline
polyline lineZ (origin pz) endpolyline

### instance showrefx lineX  surface R endinstance
### instance showrefy lineY  surface G endinstance
### instance showrefz lineZ  surface B endinstance



bank p    # init  min   max  step 
    set rad  0.1   0.01  0.3  0.01
    set tx   3.5   1     4    0.1
    set ty   1     0.2   2    0.1
    set tz   1     0     2    0.1
    set t2x  1.0   1     2    0.1
    set t2y  3.1   1     4    0.1
    set t2z  1.6     0     2    0.1
    set oz   0.4   0     1    0.01
    set rx   0.5   -10     4    0.05
    set ry   0.2     -5   2    0.1
    set rz   1.4     -1     2    0.1
   list ( rad  tx ty tz t2x t2y t2z oz rx ry rz)
endbank

point t1 ( {expr -0.5*$p.tx+0.9}  {expr -$p.ty}  {expr -0.5*$p.tz -$p.oz} ) endpoint
point t2 ( {expr  $p.tx}  {expr -$p.ty}  {expr  $p.tz} ) endpoint
point t3 ( 0.7+{expr  $p.t2x} {expr  $p.t2y} {expr -$p.t2z} ) endpoint
point t4 ( 0              {expr  0.5*$p.t2y} {expr -0.5*$p.t2z} ) endpoint
point rh2 ( {expr ($p.rx)}  {expr $p.rx * (2+0.5*$p.rx)*(0.866) - (0.2+$p.ry)*(-0.5) / (2+0.5*$p.rx)*(-0.5) + (0.2+$p.ry)*(0.866)}  {expr -0.5*$p.rz } ) endpoint
point rh1 ( {expr -0.5*$p.rx}  {expr -$p.ry}  {expr -0.5*$p.rz -2*$p.oz} ) endpoint

point h1 ( {expr -0.5*$p.tx + 0.1}  {expr 0.6-$p.ty}  {expr -0.5*$p.tz -2*$p.oz} ) endpoint
point h2 ( {expr 0.1 + 0.5*$p.tx}  {expr -$p.ty}  {expr -0.5*$p.tz + 2} ) endpoint

### rotate rh2 for 120 degrees clockwise around the z-axis
point rh2_rotated ({expr $p.rx*cos(4.189) - ($p.rx * (2+0.5*$p.rx)*(0.866) - (0.2+$p.ry)*(-0.5) / (2+0.5*$p.rx)*(-0.5) + (0.2+$p.ry)*(0.866))*sin(4.189)}  {expr $p.rx*sin(4.189) + ($p.rx * (2+0.5*$p.rx)*(0.866) - (0.2+$p.ry)*(-0.5) / (2+0.5*$p.rx)*(-0.5) + (0.2+$p.ry)*(0.866))*cos(4.189)}  {expr -0.5*$p.rz}) endpoint

### rotate t4 for 120 degrees clockwise around the z-axis
point t4_rotated ({expr -0.5*$p.t2y*sin(4.189)}  {expr 0.5*$p.t2y*cos(4.189)}  {expr -0.5*$p.t2z}) endpoint

### 0.2:0.8 point between rh2_rotated and h1
point rh2_h1_mid1 ( {expr 0.2*($p.rx*cos(4.189) - ($p.rx * (2+0.5*$p.rx)*(0.866) - (0.2+$p.ry)*(-0.5) / (2+0.5*$p.rx)*(-0.5) + (0.2+$p.ry)*(0.866))*sin(4.189)) + 0.8*(-0.5*$p.tx + 0.9)} {expr -0.3+0.2*($p.rx*sin(4.189) + ($p.rx * (2+0.5*$p.rx)*(0.866) - (0.2+$p.ry)*(-0.5) / (2+0.5*$p.rx)*(-0.5) + (0.2+$p.ry)*(0.866))*cos(4.189)) + 0.8*(0.6-$p.ty)}  {expr -1.8+0.2*(-0.5*$p.rz) + 0.8*(-0.5*$p.tz -2*$p.oz)} ) endpoint

### 0.8:0.2 point between rh2_rotated and h1
point rh2_h1_mid2 ( {expr 0.8*($p.rx*cos(4.189) - ($p.rx * (2+0.5*$p.rx)*(0.866) - (0.2+$p.ry)*(-0.5) / (2+0.5*$p.rx)*(-0.5) + (0.2+$p.ry)*(0.866))*sin(4.189)) + 0.2*(-0.5*$p.tx + 0.9)} {expr 0.8*($p.rx*sin(4.189) + ($p.rx * (2+0.5*$p.rx)*(0.866) - (0.2+$p.ry)*(-0.5) / (2+0.5*$p.rx)*(-0.5) + (0.2+$p.ry)*(0.866))*cos(4.189)) + 0.2*(0.6-$p.ty)}  {expr -1.8+0.8*(-0.5*$p.rz) + 0.2*(-0.5*$p.tz -2*$p.oz)} ) endpoint

bspline bbase order (3) (h1 t1 t2 t3 t4 rh2) segs (100) endbspline  ## visible trefoil
bspline bconn order (3) (t1 h1 rh2_h1_mid1 rh2_h1_mid2 rh2_rotated t4_rotated) segs (100) endbspline  ## hidden connectors


circle c0 ( {expr $p.rad + 0.01}  10 ) endcircle

circle c1 ( {expr $p.rad}  5 ) endcircle

sweep bTref
    crosssection c0   begincap endcap    endcrosssection  #  begincap endcap             
    path bbase   mintorsion      endpath
endsweep

sweep bConn
    crosssection c1   begincap endcap    endcrosssection  #  begincap endcap             
    path bconn   mintorsion azimuth 30    endpath
endsweep

instance ibTC  bTref   surface W   scale( 0.1 0.1 0.1 )  endinstance
instance ibTB  bTref   surface W   scale( 0.1 0.1 0.1 )   rotate(0 0 0.1)(120)   endinstance
instance ibTG  bTref   surface W   scale( 0.1 0.1 0.1 )   rotate(0 0 0.1)(240)   endinstance

instance ibCO  bConn   surface PI   scale( 0.1 0.1 0.1 )  endinstance
instance ibCR  bConn   surface PI   scale( 0.1 0.1 0.1 )   rotate(0 0 0.1)(120)   endinstance
instance ibCM  bConn   surface PI   scale( 0.1 0.1 0.1 )   rotate(0 0 0.1)(240)   endinstance
   


########################  STEM  ##########################

point s_bottom ( 0 0 10 ) endpoint
point s_reinforce ( 0 0 9.6 ) endpoint
point s_mid ( 0 1 5 ) endpoint
point origin_reinforce ( 0 0 0.4 ) endpoint

bspline stem order (3) (s_bottom s_reinforce s_mid origin_reinforce origin rh2 t4) segs (100) endbspline

sweep stemSweep
    crosssection c0  begincap endcap  endcrosssection  #  begincap endcap             
    path stem   mintorsion      endpath 
endsweep

### instance ibS  stemSweep   surface W   translate(0 0 -0.1) scale( 0.1 0.1 0.2 ) rotate(0 0 0.1)(15)   endinstance
instance ibS  stemSweep   surface W  scale( 0.1 0.1 0.1 ) endinstance